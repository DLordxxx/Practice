&НаСервере
Процедура ЗаполнитьТабличнуюЧастьСлучайнымиДаннымиНаСервере()
	
	ТабличнаяЧасть = Объект.ХарактеристикаСтудента;
	ТабличнаяЧасть.Очистить();
	
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФормыКонтроля.Дисциплина КАК Дисциплина,
		|	ФормыКонтроля.НаименованиеКонтроля КАК НаименованиеКонтроля
		|ИЗ
		|	Справочник.ФормыКонтроля КАК ФормыКонтроля
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дисциплина";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	КоличествоСтрок = ГСЧ.СлучайноеЧисло(1, 15);
	
	Для Счетчик = 1 По КоличествоСтрок Цикл
		
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		
		Если Выборка.Следующий() Тогда
			НоваяСтрока.Дисциплина = Выборка.Дисциплина;
			
			// Подбор оценки в зависимости от формы контроля
			Если Выборка.НаименованиеКонтроля = Перечисления.НаименованиеКонтроля.Экзамен Тогда
				ДоступныеОценки = ПолучитьДоступныеОценкиДляЭкзамена();
			ИначеЕсли Выборка.НаименованиеКонтроля = Перечисления.НаименованиеКонтроля.Зачет Тогда
				ДоступныеОценки = ПолучитьДоступныеОценкиДляЗачета();
			Иначе
				ДоступныеОценки = ПолучитьДоступныеОценки(); // Если форма контроля не определена, берем все оценки
			КонецЕсли;
			
			// Выбираем случайную оценку из доступных
			СлучайныйИндексОценки = ГСЧ.СлучайноеЧисло(0, ДоступныеОценки.Количество() - 1);
			НоваяСтрока.Оценка = ДоступныеОценки[СлучайныйИндексОценки];
			
		Иначе
			// Если записей в справочнике форм контроля меньше, чем количество строк, то останавливаемся
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДоступныеОценкиДляЭкзамена()
	ДоступныеОценки = Новый Массив;
	
	ДоступныеОценки.Добавить(Перечисления.Оценка.Отлично);
	ДоступныеОценки.Добавить(Перечисления.Оценка.Хорошо);
	ДоступныеОценки.Добавить(Перечисления.Оценка.Удовлетворительно);
	ДоступныеОценки.Добавить(Перечисления.Оценка.Неудовлетворительно);
	
	Возврат ДоступныеОценки;
КонецФункции

&НаСервере
Функция ПолучитьДоступныеОценкиДляЗачета()
	ДоступныеОценки = Новый Массив;
	
	ДоступныеОценки.Добавить(Перечисления.Оценка.Зачет);
	ДоступныеОценки.Добавить(Перечисления.Оценка.Незачет);
	
	Возврат ДоступныеОценки;
КонецФункции

&НаСервере
Функция ПолучитьДоступныеОценки()
	ДоступныеОценки = Новый Массив;
	МетаданныеПеречисления = Метаданные.Перечисления.Оценка;

	Для Каждого ЗначениеПеречисления Из МетаданныеПеречисления.ЗначенияПеречисления Цикл
		ДоступныеОценки.Добавить(Перечисления.Оценка[ЗначениеПеречисления.Имя]);
	КонецЦикла;

	Возврат ДоступныеОценки;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьТабличнуюЧастьСлучайнымиДанными(Команда)
	ЗаполнитьТабличнуюЧастьСлучайнымиДаннымиНаСервере();
КонецПроцедуры                                            